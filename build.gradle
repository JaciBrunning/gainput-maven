plugins {
  id 'maven-publish'
  id "edu.wpi.first.GradleRIO" version "2019.1.1-rc-1"
  id 'cpp'
  id 'base'
}

if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
  apply plugin: 'objective-cpp'
}

apply from: 'publishingtools.gradle'
apply from: 'vendordeps.gradle'

group = 'thirdparty.gainput'
version = '1.0.0'

model {
  components {
    gainput(NativeLibrarySpec) {
      targetPlatform wpi.platforms.desktop

      binaries.all {
        def mac = targetPlatform.operatingSystem.isMacOsX()
        sources {
          gainput(mac ? ObjectiveCppSourceSet : CppSourceSet) {
            source {
              srcDir 'gainput/lib/source'
              if (mac) {
                include '**/*.cpp', '**/*.mm'
              } else {
                include '**/*.cpp'
              }
            }
            exportedHeaders {
              srcDir 'gainput/lib/include'
            }
          }
        }

        if (mac) {
          linker.args << '-framework' << 'Foundation'
          linker.args << '-framework' << 'IOKit'
          linker.args << '-framework' << 'AppKit'
          linker.args << '-framework' << 'GameController'
        } else if (targetPlatform.operatingSystem.isWindows()) {
          linker.args << 'XINPUT9_1_0'
        }
      }
    }
  }
}

task zipHeaders(type: Zip) {
  from fileTree('gainput/lib/include')

  classifier = 'headers'
}

publishing {
  publications {
    gainput(MavenPublication) {
      artifactId "gainput"

      artifact zipHeaders {
          classifier 'headers'
      }

      binaryArtifacts(it, 'gainput')
    }
  }
}

wrapper {
  gradleVersion = 5.0
}